cmake_minimum_required(VERSION 3.12)

option(MFI_CROSS_COMPILE "Cross Compile for MIPS" OFF)

set(CMAKE_EXPORT_COMPILE_COMMANDS ON)

set(MIPS_TOOLCHAIN_FILE ${CMAKE_CURRENT_SOURCE_DIR}/br2/host/share/buildroot/toolchainfile.cmake)
if(MFI_CROSS_COMPILE AND ("${CMAKE_HOST_SYSTEM_NAME}" STREQUAL "Linux"))
	message(STATUS "Cross Compiling for MIPS")
	if((NOT EXISTS ${MIPS_TOOLCHAIN_FILE}) OR
	   (${CMAKE_CURRENT_SOURCE_DIR}/br2/.config IS_NEWER_THAN ${MIPS_TOOLCHAIN_FILE}))
		execute_process(
			COMMAND make O=${CMAKE_CURRENT_SOURCE_DIR}/br2
			WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/buildroot
		)
	endif()
	set(CMAKE_TOOLCHAIN_FILE ${MIPS_TOOLCHAIN_FILE})
endif()

project(mFiCustomCode)

set(MFI_API_TARGET_NAME mFiApi)

add_subdirectory(mfi)

add_executable(Hello ${CMAKE_CURRENT_SOURCE_DIR}/hello.cpp)
set_target_properties(Hello PROPERTIES
	CXX_STANDARD 20
	CXX_EXTENSIONS OFF
	MAP_IMPORTED_CONFIG_MINSIZEREL Release
	MAP_IMPORTED_CONFIG_RELWITHDEBINFO Release
	VERSION 1.0.0.0
)
target_link_libraries(Hello PUBLIC ${MFI_API_TARGET_NAME})